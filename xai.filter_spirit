# [SECTION 1] OVERRIDES
BaseType $priority_base_types {
    ItemLevel >= $priority_min_ilvl {
        Rarity <= Rare {
            Corrupted False {
                FracturedItem False {
                    ElderItem False {
                        ShaperItem False {
                            Sockets < 6 {
                                Set $comp_override
                                Show
                            }
                        }
                    }
                }
            }
        }
    }
}

BaseType $chance_base_types {
    Corrupted False {
        Mirrored False {
            FracturedItem False {
                ItemLevel >= 50 {
                    Sockets < 6 {
                        Rarity = Normal {
                            Set $comp_chance
                            Show
                        }
                    }
                }
            }
        }
    }
}

Class "Map" {
    Rarity <= Rare {
        BaseType $map_highlighting {
            Set $comp_t1_map
            Show
        }
    }
}

# [SECTION 2] LEVELING LINKS
# 4-links with colors
AreaLevel > 1 {  # makes sure we don't trigger this block in Harvest/Menagerie and similar zones
    SocketGroup = $leveling_link4_colors {
        LinkedSockets = 4 {
            Sockets < 6 {
                Rarity <= Rare {
                    Class "Gloves" "Helm" "Boots" "Body Armour" {
                        AreaLevel <= 52 {
                            Set $comp_leveling_links
                            MinimapIcon $icon_leveling_4link
                            SetAlertSound $alert_leveling_link
                            Show #TOGGLE-LEVELING
                        }
                        AreaLevel <= 67 {
                            Set $comp_leveling_links
                            Show #TOGGLE-LEVELING
                        }
                    }
                }
            }
        }
    }

    # 3-links
    SocketGroup = $leveling_link3_colors {
        LinkedSockets = 3 {
            Sockets < 6 {
                Rarity <= Rare {
                    Class "Gloves" "Helm" "Boots" "Body Armour" {
                        AreaLevel <= 25 {
                            Set $comp_leveling_links
                            MinimapIcon $icon_leveling_3link
                            SetAlertSound $alert_leveling_link
                            Show #TOGGLE-LEVELING
                        }
                        AreaLevel <= 47 {
                            Set $comp_leveling_links
                            Show #TOGGLE-LEVELING
                        }
                    }
                }
            }
        }
    }

    # 2-links
    SocketGroup = $leveling_link2_colors {
        LinkedSockets = 2 {
            Sockets < 6 {
                Rarity <= Rare {
                    AreaLevel <= 15 {
                        Class "Gloves" "Helm" "Boots" {
                            Set $comp_leveling_2l
                            Show #TOGGLE-LEVELING
                        }
                    }
                }
            }
        }
    }

    # caster weapons
    SocketGroup = $leveling_link3_colors {
        LinkedSockets = 3 {
            Rarity <= Rare {
                AreaLevel <= 25 {
                    Class $leveling_caster_weapons {
                        Set $comp_leveling_links
                        Show #TOGGLE-LEVELING
                    }
                }
            }
        }
    }

    # 4-links without specific colors
    LinkedSockets = 4 {
        Sockets < 6 {
            AreaLevel <= 52 {
                Rarity <= Rare {
                    Class "Gloves" "Helm" "Boots" "Body Armour" {
                        Set $comp_leveling_links
                        SetFontSize $font_medium
                        Show #TOGGLE-LEVELING
                    }
                }
            }
        }
    }
}

# [SECTION 3] CURRENCY
# meme items
BaseType "Fishing Rod" "Albino Rhoa Feather" {
    Set $comp_t1_currency
    Show
}

# SHAPER VOICELINES
Class $shaper_voicelines {
    BaseType "Mirror of Kalandra" {
        Set $comp_t1_currency
        SetAlertSound ShMirror 300
        Show
    }
    
    BaseType "Exalted Orb" {
        Set $comp_t1_currency
        SetAlertSound ShExalted 300
        Show
    }
    
    BaseType "Divine Orb" {
        Set $comp_t2_currency
        SetAlertSound ShDivine 300
        Show
    }
    
    BaseType "Orb of Annulment" "Harbinger's Orb" "Ancient Orb" "Exalted Shard" {
        Set $comp_t2_currency
        SetAlertSound ShGeneral 300
        Show
    }
    
    BaseType "Regal Orb" {
        Set $comp_t4_currency
        SetAlertSound ShRegal 300
        Show
    }
    
    BaseType "Chaos Orb" {
        Set $comp_t4_currency
        SetAlertSound ShChaos 300
        Show
    }
    
    BaseType "Vaal Orb" {
        Set $comp_t4_currency
        SetAlertSound ShVaal 300
        Show
    }
    
    BaseType "Orb of Alchemy" {
        Set $comp_t5_currency
        SetAlertSound ShAlchemy 300
        Show #TOGGLE-T5-CURRENCY
    }
    
    BaseType "Orb of Fusing" {
        Set $comp_t5_currency
        SetAlertSound ShFusing 300
        Show #TOGGLE-T5-CURRENCY
    }
}

# Generic currency handling
Class "Currency" {
    BaseType $currency_t1 {
        Set $comp_t1_currency
        Show
    }
    BaseType $currency_t2 {
        Set $comp_t2_currency
        Show
    }
    BaseType $currency_t3 {
        Set $comp_t3_currency
        Show
    }
    BaseType $currency_t4 {
        Set $comp_t4_currency
        Show  #TOGGLE-T4-CURRENCY
    }
    BaseType $currency_t5 {
        Set $comp_t5_currency
        StackSize >= 3 {
            Set $comp_t3_currency
            Show
        }
        
        Show #TOGGLE-T5-CURRENCY
    }
    BaseType $currency_t6 {
        Set $comp_t6_currency
        StackSize >= 3 {
            Set $comp_t4_currency
            Show #TOGGLE-T4-CURRENCY
        }
        AreaLevel <= 67 {
            SetFontSize $font_max
            Show #TOGGLE-T6-CURRENCY
        }
        
        Show #TOGGLE-T6-CURRENCY
    }
    BaseType $currency_t7 {
        Set $comp_t7_currency
        StackSize >= 3 {
            Set $comp_t5_currency
            Show #TOGGLE-T5-CURRENCY
        }
        AreaLevel <= 67 {
            SetFontSize $font_max
            Show #TOGGLE-T7-CURRENCY
        }
        
        Show #TOGGLE-T7-CURRENCY
    }
    BaseType $currency_t8 {
        Set $comp_t8_currency
        StackSize >= 3 {
            Set $comp_t6_currency
            Show #TOGGLE-T6-CURRENCY
        }
        AreaLevel <= 67 {
            SetFontSize $font_max
            Show #TOGGLE-T8-CURRENCY
        }
        
        Show #TOGGLE-T8-CURRENCY
    }
    
    BaseType "Portal Scroll" {
        Set $comp_portal
        StackSize >= 20 {
            Set $comp_t4_currency
            Show #TOGGLE-T4-CURRENCY
        }
        AreaLevel <= 67 {
            SetFontSize $font_max
            Show #TOGGLE-T8-CURRENCY
        }
        Show #TOGGLE-T8-CURRENCY
    }

    BaseType "Scroll of Wisdom" {
        Set $comp_wisdom
        StackSize >= 40 {
            Set $comp_t4_currency
            Show #TOGGLE-T4-CURRENCY
        }
        
        AreaLevel <= 67 {
            SetFontSize $font_max
            Show #TOGGLE-T9-CURRENCY
        }
        
        AreaLevel <= 72 {
            SetFontSize $font_medium
            Show #TOGGLE-T9-CURRENCY
        }
        
        Hide
    }

    BaseType "Perandus Coin"{
        StackSize >= 15{
            SetFontSize $font_max
            SetTextColor $color_perandus
            SetBorderColor $color_perandus
            PlayEffect White
            Show #TOGGLE-T4-CURRENCY
        }
        StackSize > 4 {
            SetFontSize $font_xl
            SetTextColor $color_perandus
            SetBorderColor $color_perandus
            PlayEffect White Temp
            Show #TOGGLE-T6-CURRENCY
        }
        SetFontSize $font_medium
        SetTextColor $color_perandus
        Show #TOGGLE-T7-CURRENCY
    }
}

BaseType == "Abyssal Delirium Orb" "Amorphous Delirium Orb" "Armoursmith's Delirium Orb" "Blacksmith's Delirium Orb" "Blighted Delirium Orb" "Cartographer's Delirium Orb" "Decadent Delirium Orb" "Delirium Orb" "Diviner's Delirium Orb" "Fine Delirium Orb" "Foreboding Delirium Orb" "Fossilised Delirium Orb" "Fragmented Delirium Orb" "Imperial Delirium Orb" "Jeweller's Delirium Orb" "Obscured Delirium Orb" "Portentous Delirium Orb" "Primal Delirium Orb" "Singular Delirium Orb" "Skittering Delirium Orb" "Thaumaturge's Delirium Orb" "Timeless Delirium Orb" "Whispering Delirium Orb" {
    Set $comp_t2_currency
    SetAlertSound $alert_t3_currency
    Show
}

# this really feels like currency to me -_-
BaseType "Divine Vessel" {
    Set $comp_divine_vessel
    Show
}

Class "Pantheon Soul" {
    Set $comp_divine_vessel
    Show
}

# [SECTION 4] 6L
LinkedSockets = 6 {
    Set $comp_6l

    Rarity = Unique {
        Set $comp_t1_unique
        BaseType "Simple Robe" {
            SocketGroup WWWWWW {
                # TABULA
                SetAlertSound $alert_t2_unique
                Show
            }
        }
        Show
    }
    
    Corrupted False {
        # block for good 6Ls
        SetBorderColor $red
        SetTextColor $red
        
        Class "Body Armour" {
            Show
        }
        
        Class "Bow" {
            ItemLevel >= 64 {
                Show
            }
        }
    }
    
    # rest of the 6L
    Show
}

# [SECTION 5] LEAGUES & SOME SPECIAL STUFF
# Enchanted items
AnyEnchantment True {
    Class "Helmets"{
        Set $comp_rare_t1
        SetBorderColor $color_special_rares_border
        SetAlertSound $alert_special_rares
        PlayEffect $beam_special
        MinimapIcon 2 Blue Diamond
        Show
    }
    Class "Boots" "Gloves"{
        Set $comp_rare_t1
        SetBorderColor $color_special_rares_border
        SetAlertSound $alert_special_rares
        Show
    }

}

# Warbands
HasExplicitMod "Brinerot" "Mutewind" "Redblade" "Betrayer's" "Deceiver's" "Turncoat's" {
    Set $comp_warbands
    Show
}

# Talisman
# uniques are handled in uniques section
Rarity <= Rare {
    BaseType "Talisman" {
        HasExplicitMod "Virile" "Rotund" "Athlete's" "Overpowering" "Devastating" "of Exile" "of Bameth" {
            Set $comp_good_talisman
            Show #TOGGLE-GOOD-TALISMAN
        }
    }
    BaseType "Primal Skull Talisman" "Fangjaw Talisman" "Clutching Talisman" "Horned Talisman" "Wereclaw Talisman" "Rotfeather Talisman" "Longtooth Talisman" "Spinefuse Talisman" "Three Rat Talisman" {
        Set $comp_good_talisman_base
        Show #TOGGLE-GOOD-TALISMAN
    }
    BaseType "Talisman" {
        Set $comp_talisman
        Show #TOGGLE-TALISMAN
    }
}

# Prophecy
Class "Currency"{
    Prophecy $prophecy_t1 {
        Set $comp_prophecy_t1
        Show
    }
    Prophecy $prophecy_t2 {
        Set $comp_prophecy_t2
        Show
    }
    Prophecy $prophecy_t3 {
        Set $comp_prophecy_t3
        Show
    }
    Prophecy $prophecy_t5 {
        SetFontSize $font_small
        SetTextColor $color_prophecy
        SetBackgroundColor $black
        Hide
    }
    BaseType "Prophecy" {
        Set $comp_prophecy_t4
        Show # TOGGLE-T4-PROPHECIES
    }
}

# Essences
# @TODO: just straight up copied from Alek's filter now
Class "Currency" {
    BaseType "Essence of Hysteria" "Essence of Insanity" "Essence of Horror" "Essence of Delirium" {
        SetBackgroundColor $color_t2_currency_bg
        SetBorderColor $white
        SetFontSize $font_max
        SetTextColor $white
        SetAlertSound $alert_t2_currency
        PlayEffect Green
        MinimapIcon 0 Green Circle
        Show
    }

    BaseType "Shrieking Essence" "Deafening Essence" "Remnant of Corruption" {
        SetBackgroundColor $color_t3_currency_bg
        SetBorderColor $black
        SetFontSize $font_max
        SetTextColor $black
        SetAlertSound $alert_t3_currency
        PlayEffect Green
        MinimapIcon 1 Green Circle
        Show
    }
    BaseType "Screaming Essence" {
        SetBackgroundColor $color_t4_currency_bg
        SetBorderColor $black
        SetFontSize $font_xl
        SetTextColor $black
        SetAlertSound $alert_t4_currency
        PlayEffect Green Temp
        MinimapIcon 2 Green Circle
        Show
    }
    BaseType "Wailing Essence" {
        SetBackgroundColor $color_t5_currency_bg
        SetBorderColor $black
        SetFontSize $font_xl
        SetTextColor $black
        SetAlertSound $alert_t5_currency
        Show

    }
    BaseType "Essence" {
        SetBackgroundColor $color_t6_currency_bg
        SetBorderColor $black
        SetFontSize $font_medium
        SetTextColor $black
        Show #TOGGLE-LOW-ESSENCES
    }
}

# Breach
# Breachstones are located under fragments
Class "Rings" {
    BaseType "Breach Ring"{
        # @TODO: id filtering
        Rarity <= Rare {
            SetFontSize $font_xs
            Show #TOGGLE-BREACH-RINGS
        }
    }
}

BaseType "Splinter of Chayula" {
    StackSize >= 2 {
        Set $comp_splinters_stacked
        Show
    }
    Set $comp_splinters_t1
    Show
}

BaseType "Splinter of Uul-Netol" "Splinter of Xoph" "Splinter of Esh" "Splinter of Tul" {
    StackSize >= 2 {
        Set $comp_splinters_stacked
        Show
    }
    Set $comp_splinters_t2
    Show #TOGGLE-T2-BREACH-SPLINTERS
}

BaseType "Blessing of Chayula" "Blessing of Esh" "Blessing of Xoph" "Blessing of Tul" "Blessing of Uul-Netol" {
    Set $comp_t2_currency
    Show
}

# Legacy
Class "Leaguestone" {
    SetFontSize 40
    SetTextColor 0 0 0
    SetBackgroundColor 150 250 50
    SetBorderColor 0 0 0
    PlayEffect Green Temp
    SetAlertSound 2 300
    Show
}

# Garbinger Harbinger
Class "Piece" {
    Set $comp_t3_unique
    Show
}

# Bestiary :)
# @TODO: at least for the id filtering this should probably use some compounds
BaseType "Bestiary Orb" "Imprinted Bestiary Orb" {
    SetFontSize $font_max
    SetTextColor $black
    SetBackgroundColor $color_bestiary
    SetBorderColor $black
    SetAlertSound $alert_t3
    PlayEffect Green
    Show
}

HasExplicitMod "Craiceann" "Fenumus" {
    SetFontSize $font_max
    Class "Ring" "Amulet" "Belt" {
        SetTextColor $color_bestiary
        SetBorderColor $color_bestiary
        MinimapIcon 0 Green Diamond
        PlayEffect Green
        SetAlertSound $alert_special_rares
        Show
    }
    SetTextColor $color_bestiary
    MinimapIcon 1 Green Diamond
    PlayEffect Green
    Show
}

HasExplicitMod "Farrul" "Saqawal"{
    SetFontSize $font_max
    SetTextColor $color_bestiary
    MinimapIcon 2 Green Diamond
    PlayEffect Green
    Show
}

#No tiering for nets because they are redundant, and only needs support for legacy reasons.
BaseType "Thaumaturgical Net" "Necromancy Net" "Strong Steel Net" "Simple Steel Net" "Reinforced Steel Net" "Simple Iron Net" "Reinforced Iron Net" "Strong Iron Net" "Simple Rope Net" "Reinforced Rope Net" "Strong Rope Net" {
    SetFontSize $font_medium
    SetTextColor 250 250 250
    SetBackgroundColor 125 175 0
    SetBorderColor 250 250 250
    Show
}

# Incursion
BaseType $incursion_vials_t1 {
    Set $comp_t1_currency
    SetBackgroundColor $color_incursion
    MinimapIcon 0 Red Cross
    PlayEffect Red
    Show
}
BaseType $incursion_vials_t2 {
    SetBackgroundColor $color_incursion
    SetBorderColor $black
    SetTextColor $black
    MinimapIcon 0 Red Circle
    SetFontSize $font_max
    SetAlertSound $alert_t3
    PlayEffect Red
    Show
}
BaseType "Stone of Passage" "Flashpowder Keg" {
    SetFontSize $font_max
    SetBackgroundColor 160 0 0
    SetTextColor $white
    SetBorderColor $white
    MinimapIcon 1 Red Kite
    SetAlertSound $alert_t3
    Show
}
#Good Incursion mod combos
HasExplicitMod $incursion_good_mods {
    SetFontSize $font_medium
    SetTextColor $color_incursion
    MinimapIcon 0 Red Diamond
    PlayEffect Red
    Show
}

# Delve - BLEH
Class "Currency"{
    BaseType "Prime Alchemical Resonator" "Prime Chaotic Resonator"{
        Set $comp_t2_currency
        Show
    }
    BaseType "Powerful Chaotic Resonator" "Potent Chaotic Resonator" "Primitive Chaotic Resonator"{
        Set $comp_t3_currency
        Show
    }
    BaseType "Powerful Alchemical Resonator" "Potent Alchemical Resonator" "Primitive Alchemical Resonator"{
        Set $comp_t5_currency
        Show
    }

    BaseType $fossil_t1 {
        Set $comp_t1_currency
        Show
    }
    BaseType $fossil_t2 {
        Set $comp_t2_currency
        Show
    }
    BaseType $fossil_t3 {
        Set $comp_t3_currency
        Show
    }
    BaseType $fossil_t4 {
        Set $comp_t4_currency
        Show
    }
    BaseType "Fossil" {
        Set $comp_failsafe
        Show
    }
}
HasExplicitMod "of Weaponcraft" "of Spellcraft" "of Crafting" "Subterranean" "of the Underground"{
    SetFontSize $font_xl
    SetTextColor 150 250 250
    PlayEffect Blue
    Show
}

BaseType "Reliquary Key" {
    Set $comp_t1_currency
    Show
}

# Betrayal
# Scarabs are located under the Fragments section since they are technically fragments
# @TODO: veiled mod tiering
HasExplicitMod "Veil" {
    Rarity <= Rare {
        Set $comp_rare_t1
        SetBorderColor $white
        MinimapIcon 2 White Diamond
        PlayEffect White
        SetAlertSound $alert_special_rares
        SetFontSize $font_large
        Show
    }
}

# Synthesis
SynthesisedItem True {
    Rarity <= Rare {
        Set $comp_failsafe # in case some league mechanic starts dropping Synthesized items
        Show
    }
}
FracturedItem True {
    Rarity <= Rare {
        ItemLevel >= 86 {
            Set $comp_rare_t1
            SetFontSize $font_max
            SetBorderColor $color_synthesis
            MinimapIcon 1 Blue Diamond
            SetAlertSound $alert_special_rares
            PlayEffect Blue
            Show
        }
        BaseType "Vaal Regalia" "Hubris Circlet" "Sorcerer Boots" "Sorcerer Gloves" "Titanium Spirit Shield" {
            Set $comp_rare_t1
            SetFontSize $font_max
            SetBorderColor $color_synthesis
            MinimapIcon 1 Blue Diamond
            SetAlertSound $alert_special_rares
            PlayEffect Blue
            Show
        }
        ItemLevel >= 84 {
            Set $comp_rare_t1
            SetFontSize $font_max
            SetBorderColor $color_synthesis
            MinimapIcon 1 Blue Diamond
            SetAlertSound $alert_special_rares
            PlayEffect Blue
            Show
        }
        Class "Boots" "Body Armour" "Gloves" "Jewel" "Abyss Jewel" "Amulet" "Shield" "Wand" "Sceptre" "Staves" "Bow" {
            Set $comp_rare_t1
            SetFontSize $font_max
            SetBorderColor $color_synthesis
            MinimapIcon 2 Blue Diamond
            SetAlertSound $alert_special_rares
            PlayEffect Blue
            Show
        }
        Set $comp_rare_t2
        SetBorderColor $color_synthesis
        MinimapIcon 2 Blue Diamond
        Show
    }
}

# Legion
# Incubators
Class "Incubator"{
    BaseType $incubators_t1 {
        Set $comp_t2_currency
        Show
    }
    Set $comp_t3_currency
    Show
}

# Splinters @TODO: color code
BaseType "Timeless Maraketh Splinter" "Timeless Templar Splinter" {
    StackSize >= 2 {
        Set $comp_splinters_stacked
        Show
    }
    Set $comp_splinters_t1
    Show
}
BaseType "Timeless Vaal Splinter" "Timeless Karui Splinter" "Timeless Eternal Empire Splinter"{
    StackSize >= 2 {
        Set $comp_splinters_stacked
        Show
    }
    Set $comp_splinters_t2
    Show
}

# Blight
#Oils
BaseType $oils_t1 {
    SetFontSize $font_max
    SetBackgroundColor $white
    SetBorderColor $color_oil_t1
    SetTextColor $color_oil_t1
    PlayEffect Pink
    MinimapIcon 0 Pink Cross
    SetAlertSound $alert_t1_currency
    Show
}
BaseType $oils_t2 {
    SetFontSize $font_max
    SetBackgroundColor $color_oil_t1
    SetBorderColor $white
    SetTextColor $white
    PlayEffect Pink
    MinimapIcon 0 Pink Circle
    SetAlertSound $alert_t2_currency
    Show
}
BaseType $oils_t3 {
    SetFontSize $font_max
    SetBackgroundColor $color_oil_t3
    SetBorderColor $black
    SetTextColor $black
    PlayEffect Pink
    MinimapIcon 1 Pink Circle
    SetAlertSound $alert_t3_currency
    Show
}
BaseType $oils_t4 {
    SetFontSize $font_medium
    SetBackgroundColor $color_oil_t4
    SetBorderColor $black
    SetTextColor $black
    Show #TOGGLE-T4-OILS
}


# [SECTION 6] UNIQUES
# @TODO: don't forget unique watchstone, synthesis items & talisman

# [SECTION 7] DIVINATION CARDS

# [SECTION 8] MAPS

# [SECTION 9] INFLUENCED ITEMS

# [SECTION 10] LINKS & SOCKETS

# [SECTION 11] ID HIGHLIGHTING

# [SECTION 12] BASES
Rarity <= Magic {
    # highest ele damage with attacks, +3 minions and %es
    ItemLevel >= 86 {
        BaseType $bases_energy_shield_t1 $bases_evasion_energy_shield_t1 {
            Show #TOGGLE-ES-BASES
        }

        BaseType $bases_helmets_t1 {
            Show #TOGGLE-HIGH-BASES
        }
    }

    ItemLevel >= 84 {
        BaseType $bases_energy_shield_t1 $bases_evasion_energy_shield_t1 {
            Show #TOGGLE-ES-BASES
        }
    }

    # some random high rolls like T1 +attributes and chance to block
    ItemLevel >= 85 {
        BaseType $bases_helmets_t1 $bases_boots_t1 $bases_gloves_t1 $bases_body_t1 {
            Show #TOGGLE-HIGH-BASES
        }
    }

    # Highest defense rolls & caster weapons
    ItemLevel >= 84 {
        BaseType $bases_helmets_t1 $bases_boots_t1 $bases_gloves_t1 $bases_body_t1 {
            Show #TOGGLE-HIGH-BASES
        }
    }

    # merciless roll
    ItemLevel >= 83 {
        # @TODO: split these up, 2h should probably be a bit smaller
        BaseType $bases_2h_melee $bases_1h_melee {
            SetBorderColor 255 122 0
            Show #TOGGLE-HIGH-BASES
        }
    }

    ItemLevel >= 73 {
        BaseType $bases_evasion_energy_shield_t1 {
            Hide #TOGGLE-EARLY-TRICKSTER-CI
        }
    }
}

# [SECTION 13] RARES
Rarity = Rare {

}

# [SECTION 14] CHROMES

# [SECTION 15] FLASKS

# [SECTION 16] GEMS

# [SECTION 17] LEVELING

# [SECTION 18] QUEST SHIT
Class "Quest Items"{
    SetFontSize $font_max
    SetTextColor $white
    SetBackgroundColor $quest_green
    SetBorderColor $white
    MinimapIcon 0 Green Kite
    PlayEffect Green
    Show
}

BaseType "Watchstone" {
    SetTextColor $white
    SetBackgroundColor $quest_green
    SetBorderColor $white
    SetFontSize $font_max
    PlayEffect $beam_quest
    PlayAlertSound $alert_t3_currency
    Show
}

Class "Labyrinth Item" "Labyrinth Map Item" "Labyrinth Trinket"{
    SetFontSize 45
    SetTextColor $white
    SetBackgroundColor $quest_green
    SetBorderColor $white
    MinimapIcon 0 Green Kite
    PlayEffect Green
    Show
}

BaseType "Offering to the Goddess"{
    SetFontSize 45
    SetTextColor $white
    SetBackgroundColor $quest_green
    SetBorderColor $white
    MinimapIcon 0 Green Kite
    PlayEffect Green
    SetAlertSound $alert_t3_currency
    Show
}

# [SECTION 19] FRAGMENTS
# @TODO: don't forget scarabs & Breachstones

# [SECTION 20] MISC

# [SECTION 21] SAFETY NETS

# [SECTION 22] FAILSAFE & HIDE ALL
Rarity <= Magic {
    SetFontSize 22
    SetBackgroundColor 0 0 0 200
    DisableDropSound
    Hide
}

Class "Amulets" "Belts" "Body Armour" "Boots" "Bows" "Claws" "Daggers" "Flask" "Gloves" "Helmets" "Jewel" "One Hand" "Quivers" "Rings" "Rune Dagger" "Sceptre" "Shields" "Staves" "Two Hand" "Wand" "Warstaves" {
    SetFontSize 18
    DisableDropSound
    Hide
}

Set $comp_failsafe
Show
